Option Compare Database

Public modifying As Boolean

Private Sub btnPreviousEquipment_Click()
    'Move to the previous record in the Disabilities Recordset
    GlobalVariables.currentEquipment.MovePrevious
    
    'Update the record number caption
    Me.lblRecord.Caption = "Record " & GlobalVariables.currentEquipment.AbsolutePosition + 1 & " of " & GlobalVariables.currentEquipment.RecordCount
        
    'If this is the first record, make the previous button invisible
    If GlobalVariables.currentEquipment.AbsolutePosition = 0 Then
        Me.cbEquipmentType.SetFocus
        Me.btnPreviousEquipment.Visible = False
    End If
    
    'Turn the next button visible
    If GlobalVariables.currentEquipment.AbsolutePosition + 1 < GlobalVariables.currentEquipment.RecordCount Then
        Me.btnNextEquipment.Visible = True
    End If

    PopulateInformation
End Sub

Private Sub btnNewEquipment_Click()
    If modifying Then
        modifying = False
        Me.cbEquipmentType = ""
        Me.tfCost = ""
        Me.chklblPurchased.Caption = "o"
    
        'Set the disability selected to the end
        GlobalVariables.currentEquipment.Move GlobalVariables.currentEquipment.RecordCount
    
        'Change visibility of the buttons
        Me.cbEquipmentType.SetFocus
        Me.btnPreviousEquipment.Visible = True
        Me.btnNextEquipment.Visible = False
    
        'Update the record number caption
        Me.lblRecord.Caption = "Record " & GlobalVariables.currentEquipment.RecordCount + 1 & " of " & GlobalVariables.currentEquipment.RecordCount + 1
    End If
End Sub

Private Sub btnNextEquipment_Click()
    'Move to the next record in the Disabilities Recordset
    GlobalVariables.currentEquipment.MoveNext
    
    'Update the record number caption
    Me.lblRecord.Caption = "Record " & GlobalVariables.currentEquipment.AbsolutePosition + 1 & " of " & GlobalVariables.currentEquipment.RecordCount
    
    'If this is the last record, turn the next button invisible
    If GlobalVariables.currentEquipment.AbsolutePosition + 1 = GlobalVariables.currentEquipment.RecordCount Then
        Me.cbEquipmentType.SetFocus
        Me.btnNextEquipment.Visible = False
    End If
            
    'Make the Previous button visible
    If GlobalVariables.currentEquipment.AbsolutePosition > 0 Then
        Me.btnPreviousEquipment.Visible = True
    End If
    
    PopulateInformation
End Sub

Private Sub chklblPurchased_Click()
    Dim checked As Boolean
    checked = Me.chklblPurchased.Caption = "þ"
    
    If Not checked Then
        Me.chklblPurchased.Caption = "þ"
    Else
        Me.chklblPurchased.Caption = "o"
    End If
End Sub

Private Sub Form_Load()
    'Declare and set variables for the client's name and participant ID
    Dim clientName As String
    Dim currentCaseID As Integer
    clientName = GlobalVariables.currentClient!First_Name & " " & GlobalVariables.currentClient!Last_Name
    currentCaseID = Me.Parent.currentCaseID
    
    'Set the client name and participant ID on the subform
    Me.tfClientName = clientName
    Me.tfCaseNumber = currentCaseID
    
    'Declare variables to look up this client's disabilities
    Dim database As DAO.database
    Set database = CurrentDb()
    Dim results As DAO.Recordset
    Dim qDef As DAO.QueryDef
    Set qDef = database.CreateQueryDef("")
    
    'Assign the SQL for the query
    qDef.SQL = "SELECT Equipment.ID, ValidEquipment.Equipment_Type, Equipment.Equipment_Cost, Equipment.Purchased " _
        & "FROM (Equipment LEFT JOIN ValidEquipment ON Equipment.Equipment_Type = ValidEquipment.ID) LEFT JOIN Cases ON Equipment.Case_ID = Cases.ID " _
        & "WHERE Cases.ID = [Param1];"
    
    qDef.Parameters("Param1").Value = currentCaseID
    
    'Run the query
    Set GlobalVariables.currentEquipment = qDef.OpenRecordset
    
    Me.btnPreviousEquipment.Visible = False
    If GlobalVariables.currentEquipment.EOF Then
        'If there are no disabilities recorded, leave fields blank and disable buttons to indicate no disabilities
        modifying = False
        Me.btnNextEquipment.Visible = False
    Else
        'If there are any disabilities recorded, display the first one
        PopulateInformation
        
        'Check the number of records left
        GlobalVariables.currentEquipment.MoveLast
        GlobalVariables.currentEquipment.MoveFirst
        
        'Update the record number caption
        Me.lblRecord.Caption = "Record " & GlobalVariables.currentEquipment.AbsolutePosition + 1 & " of " & GlobalVariables.currentEquipment.RecordCount
        
        'If this is the last record, turn the next button invisible
        If GlobalVariables.currentEquipment.AbsolutePosition + 1 = GlobalVariables.currentEquipment.RecordCount Then
            Me.cbEquipmentType.SetFocus
            Me.btnNextEquipment.Visible = False
        End If
    End If
End Sub

Private Sub PopulateInformation()
    modifying = True
    Me.cbEquipmentType = GlobalVariables.currentEquipment!Equipment_Type
    Me.tfCost = GlobalVariables.currentEquipment!Equipment_Cost
    Me.chklblPurchased.Caption = IIf(GlobalVariables.currentEquipment!Purchased = True, "þ", "o")
End Sub


